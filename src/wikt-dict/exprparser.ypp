/*
See http://dinosaur.compilertools.net/bison/bison_5.html.
*/

%skeleton "lalr1.cc"      
%defines
%define "parser_class_name" "ExprParser"

%{
#include <math.h>
#include "exprdriver.h"

#define yylex exprlex
%}

%parse-param { ExprDriver& driver }
%lex-param   { ExprDriver& driver }
%locations
%debug
%error-verbose

/* This defines the type of yylval */
%union {
  double num;
}

%{
yy::ExprParser::token_type exprlex(yy::ExprParser::semantic_type* yylval,
        yy::ExprParser::location_type* /*yylloc*/, ExprDriver& /*driver*/);
%} 

/* BISON Declarations */
%token END 0 "end of file"
%token NUM LEFT_PAREN RIGHT_PAREN
%token E PI
%left OR AND
%left EQUALS NEQUALS LARGER LESSER LESSER_EQUAL LARGER_EQUAL
%left ROUND
%left MINUS PLUS
%left MULTIPLY DIVIDE MOD
%right POW
%left NOT CEIL TRUNC FLOOR EXP ABS LN SIN COS TAN ACOS ASIN ATAN
%left NEG POS /* unary plus and minus */

%type <num> input exp NUM E PI
 
%start input
 
%% /* Grammar rules and actions follow */

input:  exp END                { $$ = $1; driver.setResult($1); };

exp:      NUM                  { $$ = $1;          }
        | exp PLUS exp         { $$ = $1 + $3;     }
        | exp MINUS exp        { $$ = $1 - $3;     }
        | MINUS exp %prec NEG  { $$ = -$2;         }
        | PLUS  exp %prec POS  { $$ = $2;          }
        | exp LESSER exp       { $$ = ($1 < $3 ? 1 : 0); }
        | exp LARGER exp       { $$ = ($1 > $3 ? 1 : 0); }
        | exp LESSER_EQUAL exp { $$ = ($1 <= $3 ? 1 : 0); }
        | exp LARGER_EQUAL exp { $$ = ($1 >= $3 ? 1 : 0); }
        | exp MULTIPLY exp     { $$ = $1 * $3; }
        | exp DIVIDE exp       { $$ = $1 / $3; }
        | LEFT_PAREN exp RIGHT_PAREN { $$ = $2; }
        | exp ROUND exp        { double i = pow(10, $3); $$ = round($1 * i) / i; }
        | exp POW exp          { $$ = pow($1, $3); }
        | exp MOD exp          { $$ = (int)$1 % (int)$3; }
        | exp EQUALS exp       { $$ = ($1 == $3 ? 1 : 0); }
	| exp NEQUALS exp      { $$ = ($1 != $3 ? 1 : 0); }
	| exp AND exp 	       { $$ = ($1 && $3 ? 1 : 0); }
	| exp OR exp	       { $$ = ($1 || $3 ? 1 : 0); }
	| NOT exp	       { $$ = ($2 ? 0 : 1); }
	| CEIL exp	       { $$ = ceil($2); }
	| FLOOR exp	       { $$ = floor($2); }
	| ABS exp	       { $$ = ($2 > 0 ? $2 : -$2); }
	| LN exp	       { $$ = log($2); }
	| TRUNC exp	       { $$ = trunc($2); }
	| EXP exp	       { $$ = exp($2); }
	| SIN exp	       { $$ = sin($2); }
	| COS exp	       { $$ = cos($2); }
	| TAN exp	       { $$ = tan($2); }
	| ASIN exp	       { $$ = asin($2); }
	| ACOS exp	       { $$ = acos($2); }
	| ATAN exp	       { $$ = atan($2); }
	| PI   		       { $$ = 3.1415926535898; }
	| E 		       { $$ = 2.718281828459; }
;
        
%%
void yy::ExprParser::error(const yy::ExprParser::location_type& l, const std::string& m)
{
  driver.error(l, m);
}
